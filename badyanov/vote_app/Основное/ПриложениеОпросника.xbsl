@НаКлиенте
@НаСервере
структура ДанныеОпроса
    пер ОпросСсылка: Опросы.Ссылка?
    пер ТекстВопроса: Строка
    пер ВариантыОтветов: Массив<Строка>
;

@Обработчик
метод ПриОткрытииПоСсылке(Событие: СобытиеПриОткрытииПоСсылке)
    пер ДанныеОпроса: ДанныеОпроса?
    если Событие.Ссылка.Параметры != Неопределено и Событие.Ссылка.Параметры.Содержит("poll")
        ДанныеОпроса = ДанныеОпросаНаСервере(Событие.Ссылка.Параметры.ПолучитьПервый("poll"))
    ;
    если ДанныеОпроса == Неопределено
        ОтветУчтен = Истина
        выбросить новый ИсключениеВалидации("Некорректная ссылка")
    ;

    ФиоВведен = Ложь
    ОпросСсылка = ДанныеОпроса.ОпросСсылка
    ТекстВопроса = ДанныеОпроса.ТекстВопроса
    для ВариантОтвета из ДанныеОпроса.ВариантыОтветов
        (Компоненты.ГруппаРадиоКнопок.СписокВыбора как Массив<ЭлементСпискаЗначений<Строка> | Строка>).Добавить(
            новый ЭлементСпискаЗначений<Строка>(Значение = ВариантОтвета)
        )
    ;
;

@НаСервере
@ДоступноСКлиента
статический метод ДанныеОпросаНаСервере(ИдентификаторОпроса: Строка): ДанныеОпроса?
    знч Результат = новый ДанныеОпроса()

    знч ОпросОбъект = Опросы.ПолучитьСсылку(новый Ууид(ИдентификаторОпроса)).ЗагрузитьОбъект()
    если ОпросОбъект == Неопределено
        возврат Неопределено
    ;

    Результат.ОпросСсылка = ОпросОбъект.Ссылка
    Результат.ТекстВопроса = ОпросОбъект.ТекстВопроса
    для СтрокаТЧ из ОпросОбъект.ВариантыОтветов
        Результат.ВариантыОтветов.Добавить(СтрокаТЧ.Ответ)
    ;

    возврат Результат
;

метод ВвестиФио(Источник: Кнопка, Событие: СобытиеПриНажатии)
    ФиоВведен = Истина
;

метод ЗавершитьОпрос(Источник: Кнопка, Событие: СобытиеПриНажатии)
    если Диалог.Вопрос(Text="Завершить опрос", Buttons=[СтандартнаяКнопкаДиалога.Да, СтандартнаяКнопкаДиалога.Нет], Explanation="Вы уверены?") == СтандартнаяКнопкаДиалога.Да
        ОтветУчтен = Истина
        знч ОтветСохранен = ЗаписатьРезультатОпроса(ОпросСсылка, ФИО, ТекстОтвета)
        Компоненты.НадписьИтоговая.Значение = ОтветСохранен 
            ? "Спасибо! Ваш ответ учтен :)"
            : "Возникла ошибка при сохранении результатов, обратитесь к разработчику"
    ;
;

@Локально
@НаСервере
@ДоступноСКлиента
статический метод ЗаписатьРезультатОпроса(Опрос: Опросы.Ссылка, ФИО: Строка, Ответ: Строка): Булево
    знч НоваяЗапись = новый РезультатыОпросов.Запись(
        Опрос = Опрос,
        ФИО = ФИО,
        Ответ = Ответ,
        Дата = ДатаВремя.Сейчас()
    )
    
    попытка
        исп КонтекстДоступа.Привилегированный()
        РезультатыОпросов.Записать(НоваяЗапись)
        возврат Истина
    поймать ИсключениеПриЗаписи: Исключение
        новый ОшибкаЗаписиРегистра(
                Опрос = Опрос.Представление(),
                ОпросУуид = Опрос.Ид.ВСтроку(),
                ОписаниеОшибки = "%{ИсключениеПриЗаписи.Описание}").Записать()
    ;

    возврат Ложь
;